#include <SPI.h>
#include <MFRC522.h>
#include <Servo.h>
#include <LiquidCrystal.h>

// RFID
#define SS_PIN 10
#define RST_PIN 9
MFRC522 rfid(SS_PIN, RST_PIN);

// Servo
#define SERVO_PIN A0
Servo lockServo;

// LEDs
#define GREEN_LED 8
#define RED_LED A1

// Buzzer
#define BUZZER_PIN A2

// LCD (RS, E, D4, D5, D6, D7)
LiquidCrystal lcd(2, 3, 4, 5, 6, 7);

// Authorized UIDs
byte authorized1[] = {0xBC, 0x7F, 0x6D, 0x05};  // Default
byte authorized2[] = {0xFA, 0x27, 0xC, 0x7A};   // Julio's card

bool compareUID(byte *a, byte *b) {
  for (byte i = 0; i < 4; i++) {
    if (a[i] != b[i]) return false;
  }
  return true;
}

void setup() {
  Serial.begin(9600);
  SPI.begin();
  rfid.PCD_Init();

  lockServo.attach(SERVO_PIN);
  lockServo.write(0);  // Locked

  pinMode(GREEN_LED, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  pinMode(BUZZER_PIN, OUTPUT);

  lcd.begin(16, 2);
  lcd.print("Scan your card");
}

void loop() {
  if (!rfid.PICC_IsNewCardPresent() || !rfid.PICC_ReadCardSerial()) {
    return;
  }

  byte *uid = rfid.uid.uidByte;
  Serial.print("Card UID: ");
  for (byte i = 0; i < rfid.uid.size; i++) {
    Serial.print(uid[i], HEX);
    Serial.print(" ");
  }
  Serial.println();

  bool isUser1 = compareUID(uid, authorized1);
  bool isJulio = compareUID(uid, authorized2);

  if (isUser1 || isJulio) {
    Serial.println("Access Granted");
    lcd.clear();
    if (isJulio) {
      lcd.print("Welcome, Julio");
    } else {
      lcd.print("Access Granted");
    }

    digitalWrite(GREEN_LED, HIGH);
    digitalWrite(RED_LED, LOW);
    tone(BUZZER_PIN, 1000, 200);
    lockServo.write(90);  // Unlock
    delay(3000);
    lockServo.write(0);   // Lock again
    digitalWrite(GREEN_LED, LOW);
    lcd.clear();
    lcd.print("Scan your card");
  } else {
    Serial.println("Access Denied");
    lcd.clear();
    lcd.print("Access Denied");

    digitalWrite(RED_LED, HIGH);
    digitalWrite(GREEN_LED, LOW);
    for (int i = 0; i < 3; i++) {
      tone(BUZZER_PIN, 500, 150);
      delay(200);
    }

    digitalWrite(RED_LED, LOW);
    lcd.clear();
    lcd.print("Scan your card");
  }

  rfid.PICC_HaltA();
  rfid.PCD_StopCrypto1();
}
